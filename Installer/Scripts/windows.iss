; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Non-commercial use only

; Some items must be passed at build time
; AppName
; AppPublisher
; AppReleaseURL
; AppSupportURL
; AppURL
; AppVersion
; BuildPath
; PackagePrefix
; Platform
; TargetPath

#define AppExeName AppName + ".exe"
#define AppAssocName0 "Chart File"
#define AppAssocExt0 ".chart"
#define AppAssocKey0 StringChange(AppAssocName0, " ", "") + AppAssocExt0
#define AppAssocName1 "Moonscraper Chart File"
#define AppAssocExt1 ".msce"
#define AppAssocKey1 StringChange(AppAssocName1, " ", "") + AppAssocExt1
#define InstallerName PackagePrefix + " v" + AppVersion + " Installer " + Platform

#define ArchitecturesAllowed ""
#if Platform == "x64"
  #define ArchitecturesAllowed "x64compatible"
#endif

[Setup]
AppId={{7510F674-07C4-4D9A-BAA7-B380238F6723}
AppName={#AppName}
AppVersion={#AppVersion}
;AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppSupportURL}
AppUpdatesURL={#AppReleaseURL}
DefaultDirName={autopf}\{#AppName}
UninstallDisplayIcon={app}\{#AppExeName}
ArchitecturesAllowed={#Platform}compatible
ArchitecturesInstallIn64BitMode={#ArchitecturesAllowed}
ChangesAssociations=yes
DisableProgramGroupPage=yes
LicenseFile=..\..\LICENSE
; Remove the following line to run in administrative install mode (install for all users).
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir={#BuildPath}
OutputBaseFilename={#InstallerName}
SolidCompression=yes
WizardStyle=modern
SetupIconFile=..\Icons\msce_icon.ico

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#TargetPath}\{#AppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#TargetPath}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "Custom Resources,Config";
; The files in these folders can be configured by users. Try to not nuke their changes upon upgrading.
Source: "{#TargetPath}\Custom Resources\*"; DestDir: "{app}\Custom Resources"; Flags: ignoreversion recursesubdirs createallsubdirs onlyifdoesntexist
Source: "{#TargetPath}\Config\*"; DestDir: "{app}\Config"; Flags: ignoreversion recursesubdirs createallsubdirs onlyifdoesntexist
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKA; Subkey: "Software\Classes\{#AppAssocExt0}\OpenWithProgids"; ValueType: string; ValueName: "{#AppAssocKey0}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#AppAssocKey0}"; ValueType: string; ValueName: ""; ValueData: "{#AppAssocName0}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#AppAssocKey0}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#AppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#AppAssocKey0}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#AppExeName}"" ""%1"""

Root: HKA; Subkey: "Software\Classes\{#AppAssocExt1}\OpenWithProgids"; ValueType: string; ValueName: "{#AppAssocKey1}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#AppAssocKey1}"; ValueType: string; ValueName: ""; ValueData: "{#AppAssocName1}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#AppAssocKey1}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#AppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#AppAssocKey1}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#AppExeName}"" ""%1"""

[Icons]
Name: "{autoprograms}\{#AppName}"; Filename: "{app}\{#AppExeName}"
Name: "{autodesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(AppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
